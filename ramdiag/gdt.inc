;       GDT для PM/LM
;       7 дескрипторов

;       0       -       не используется
;       1       -       код для PM
;       2       -       данные для PM
;       3       -       код для LM
;       4       -       данные для LM
;       5,6     -       TSS64 для LM (16 байт!)


cTSS_Type       =       1001b
cGDT_Size       =       GDT_End-GDT

;       структура определения дескриптора 64-битного TSS
struc   DEFINE_TSS_DESCR_64 BaseAddress, Limit
{
        dd      (Limit and 0FFFFh) or ((BaseAddress and 0FFFFh) shl 16)
        dd      ((BaseAddress shr 16) and 0FFh) or (cTSS_Type shl 8) or (8 shl 12) or (Limit and 0F0000h) or (BaseAddress and 0FF000000h)
        dd      BaseAddress shr 32
        dd      0
}

align   8

GDT:
;       НУЛЕВОЙ ДЕСКРИПТОР
        dq 0

;       ДЕСКРИПТОР СЕГМЕНТА КОДА ДЛЯ PM (лимит 4Gb, база - 0)

        dw 1111111111111111b ; биты 15-0 лимита
        dw 0000000000000000b ; биты 15-0 базы
        db 00000000b         ; биты 23-16 базы
        db 10011010b         ; байт доступа
;          │└┤│││││
;          │ │││││└──--------- бит доступа (1 - к сегменту было обращение)
;          │ ││││└───--------- бит разрешения чтения для кода, записи для данных
;          │ │││└────--------- бит подчиненности для кода, расширения вниз для данных
;          │ ││└─────--------- тип сегмента (0 - данные, 1 - код)
;          │ │└──────--------- тип дескриптора (0 - системный, 1 - не системный)
;          │ └───────--------- уровень привилегий дескриптора (DPL)
;          └─────────--------- бит присутствия сегмента в памяти
        db 11001111b
;          ││││└──┤
;          ││││   └──--------- биты 19-16 лимита
;          │││└──────--------- зарезервировано для ОС
;          ││└───────--------- должен быть 0
;          │└────────--------- бит разрядности (1 - 32 бита, 0 - 16 бит)
;          └─────────--------- гранулярность (0 - в байтах, 1 - в 4К страницах)
        db 00000000b         ; биты 31-24 базы

;       ДЕСКРИПТОР СЕГМЕНТА ДАННЫХ ДЛЯ PM (лимит 4Gb, база - 0)

        dw 1111111111111111b ; биты 15-0 лимита
        dw 0000000000000000b ; биты 15-0 базы
        db 00000000b         ; биты 23-16 базы
        db 10010010b         ; байт доступа
;          │└┤│││││
;          │ │││││└──--------- бит доступа (1 - к сегменту было обращение)
;          │ ││││└───--------- бит разрешения чтения для кода, записи для данных
;          │ │││└────--------- бит подчиненности для кода, расширения вниз для данных
;          │ ││└─────--------- тип сегмента (0 - данные, 1 - код)
;          │ │└──────--------- тип дескриптора (0 - системный, 1 - не системный)
;          │ └───────--------- уровень привилегий дескриптора (DPL)
;          └─────────--------- бит присутствия сегмента в памяти
        db 11001111b
;          ││││└──┤
;          ││││   └──--------- биты 19-16 лимита
;          │││└──────--------- зарезервировано для ОС
;          ││└───────--------- должен быть 0
;          │└────────--------- бит разрядности (1 - 32 бита, 0 - 16 бит)
;          └─────────--------- гранулярность (0 - в байтах, 1 - в 4К страницах)
        db 00000000b         ; биты 31-24 базы


;       ДЕСКРИПТОР СЕГМЕНТА КОДА ДЛЯ LM

        dw 0000000000000000b ; биты 15-0 лимита (в LM лимит игнорируется)
        dw 0000000000000000b ; биты 15-0 базы   (в LM база игнорируется)
        db 00000000b         ; биты 23-16 базы  (в LM база игнорируется)
        db 10011000b         ; байт доступа     (не игногрируется в LM)
;          │└┤│││││
;          │ │││││└──--------- бит доступа (1 - к сегменту было обращение)
;          │ ││││└───--------- бит разрешения чтения для кода, записи для данных
;          │ │││└────--------- бит подчиненности для кода, расширения вниз для данных
;          │ ││└─────--------- тип сегмента (0 - данные, 1 - код)
;          │ │└──────--------- тип дескриптора (0 - системный, 1 - не системный)
;          │ └───────--------- уровень привилегий дескриптора (DPL)
;          └─────────--------- бит присутствия сегмента в памяти
        db 00100000b
;          ││││└──┤
;          ││││   └──--------- биты 19-16 лимита (в LM лимит игнорируется)
;          │││└──────--------- зарезервировано для ОС  (AVL)
;          ││└───────--------- 21 L (если 0 - код в lagacy mode, 1 - 64 бит )
;          │└────────--------- 22 D (0 - размер операнда по умолчанию 16, 1 - 32)
;          │                   одновременное включение L и D зарезервировано
;          └─────────--------- 23 G гранулярность (0 - в байтах, 1 - в 4К страницах) (для LM игнорирутется)
        db 00000000b         ; биты 31-24 базы  (в LM база игнорируется)

;       ДЕСКРИПТОР СЕГМЕНТА ДАННЫХ ДЛЯ LM

        dw 0000000000000000b ; биты 15-0 лимита (в LM лимит игнорируется)
        dw 0000000000000000b ; биты 15-0 базы   (в LM база игнорируется)
        db 00000000b         ; биты 23-16 базы  (в LM база игнорируется)
        db 10010010b         ; байт доступа     (не игногрируется в LM)
;          │└┤│││││
;          │ │││││└──--------- бит доступа (1 - к сегменту было обращение)
;          │ ││││└───--------- бит разрешения чтения для кода, записи для данных
;          │ │││└────--------- бит подчиненности для кода, расширения вниз для данных
;          │ ││└─────--------- тип сегмента (0 - данные, 1 - код)
;          │ │└──────--------- тип дескриптора (0 - системный, 1 - не системный)
;          │ └───────--------- уровень привилегий дескриптора (DPL)
;          └─────────--------- бит присутствия сегмента в памяти
        db 00000000b         ; игнорируется в LM
;          ││││└──┤
;          ││││   └──--------- биты 19-16 лимита (в LM лимит игнорируется)
;          │││└──────--------- зарезервировано для ОС  (AVL)
;          ││└───────--------- 21 L (если 0 - код в lagacy mode, 1 - 64 бит )
;          │└────────--------- 22 D (0 - размер операнда по умолчанию 16, 1 - 32)
;          │                   одновременное включение L и D зарезервировано
;          └─────────--------- 23 G гранулярность (0 - в байтах, 1 - в 4К страницах) (для LM игнорирутется)
        db 00000000b         ; биты 31-24 базы  (в LM база игнорируется)

TSS64Descr      DEFINE_TSS_DESCR_64 TSS_64, cTSS_Size_64-1

GDT_End:
